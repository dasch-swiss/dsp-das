appUrl := "https://app.dev.dasch.swiss"
testToRun := "first-page-load"

# List all the available targets
default:
    @just --list

# List all the available tests
list:
    @echo "Choose a test from below:"
    @echo ""
    @ls -1 tests|sed 's/\.js//'|awk '{print " * " $0}'
    @echo ""
    @echo "Run the test using 'just run <testName>'"

# Run the test locally
run testToRun=testToRun:
    APP_URL={{appUrl}} k6 run tests/{{testToRun}}.js

# Run the test in k6 cloud
run-cloud testToRun=testToRun:
    APP_URL={{appUrl}} k6 cloud tests/{{testToRun}}.js

# Start grafana
grafana-up:
    docker compose up -d
    open http://localhost:3000/dashboard/new?orgId=1
    cat ./grafana_dashboard.json | pbcopy

# Stop grafana
grafana-down:
    docker compose down

# Run the test and export to local grafana
run-grafana testToRun=testToRun:
    K6_OUT=influxdb=http://localhost:8086/k6 APP_URL={{appUrl}} k6 run ./tests/{{testToRun}}.js

