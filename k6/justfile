appUrl := "https://app.dev.dasch.swiss"
testToRun := "first-page-load"
#k6_browser_headless := "false"
k6_browser_headless := "true"

# List all the available targets
default:
    @just --list

# List all the available tests
list:
    @echo "Choose a test from below:"
    @echo ""
    @ls -1 tests|sed 's/\.js//'|awk '{print " * " $0}'
    @echo ""
    @echo "Run the test using 'just run <testName>'"

# Run the test locally
run testToRun=testToRun k6_browser_headless=k6_browser_headless:
    K6_BROWSER_HEADLESS={{k6_browser_headless}} APP_URL={{appUrl}} k6 run tests/{{testToRun}}.js

# Run all tests locally
run-all:
    APP_URL={{appUrl}} k6 run tests/first-page-load.js
    APP_URL={{appUrl}} k6 run tests/load-project-overview.js
    APP_URL={{appUrl}} k6 run tests/load-resource.js
    APP_URL={{appUrl}} k6 run tests/load-single-resource.js
    APP_URL={{appUrl}} k6 run tests/workflow-login-success.js

# Run the test in k6 cloud
run-cloud testToRun=testToRun:
    k6 cloud tests/{{testToRun}}.js

# Start grafana
grafana-up:
    docker compose up -d
    open http://localhost:3000/dashboard/new?orgId=1
    cat ./grafana_dashboard.json | pbcopy

# Stop grafana
grafana-down:
    docker compose down

# Run the test and export to local grafana
run-grafana testToRun=testToRun:
    K6_OUT=influxdb=http://localhost:8086/k6 APP_URL={{appUrl}} k6 run ./tests/{{testToRun}}.js

# Remove all screenshots
clean:
    rm screenshots/*.png

# Performance comparison commands
# Run test on DEV-02 environment (your refactor branch)
run-dev02 testToRun=testToRun:
    K6_BROWSER_HEADLESS={{k6_browser_headless}} APP_URL=https://app.dev-02.dasch.swiss k6 run tests/{{testToRun}}.js

# Run test on STAGE environment (current main branch)
run-stage testToRun=testToRun:
    K6_BROWSER_HEADLESS={{k6_browser_headless}} APP_URL=https://app.stage.dasch.swiss k6 run tests/{{testToRun}}.js

# Run test on DEV-02 with Grafana output
run-dev02-grafana testToRun=testToRun:
    K6_OUT=influxdb=http://localhost:8086/k6 APP_URL=https://app.dev-02.dasch.swiss k6 run ./tests/{{testToRun}}.js --tag env=dev02

# Run test on STAGE with Grafana output  
run-stage-grafana testToRun=testToRun:
    K6_OUT=influxdb=http://localhost:8086/k6 APP_URL=https://app.stage.dasch.swiss k6 run ./tests/{{testToRun}}.js --tag env=stage

# Compare performance: run same test on both environments
compare testToRun=testToRun:
    @echo "ðŸ”¬ Performance Comparison: {{testToRun}}"
    @echo "=================================="
    @echo "ðŸŸ¡ Testing STAGE environment (current main)..."
    just run-stage {{testToRun}}
    @echo ""
    @echo "ðŸ”µ Testing DEV-02 environment (your refactor)..."
    just run-dev02 {{testToRun}}
    @echo ""
    @echo "âœ… Comparison complete! Check the results above."
